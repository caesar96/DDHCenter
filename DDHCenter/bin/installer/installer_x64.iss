; Script generated by the Inno Script Studio Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "DDH Center!"
#define MyAppVersion "1.0.0.0"
#define MyAppPublisher "DDH Center, Inc."
#define MyAppURL "http://ddhcenter.ga"
#define MyAppExeName "DDHCenter.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{7B1487E2-7628-42A3-ACE3-8B55F220E029}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={commonpf64}\DDH Center
DefaultGroupName=DDH Center
AllowNoIcons=yes
OutputDir=.\
OutputBaseFilename=installer_x64
SetupIconFile=..\..\Ui\Images\ddhcenter_logo.ico
Compression=lzma
SolidCompression=yes
WizardSmallImageFile=Small.bmp
WizardImageFile=Large.bmp
DisableWelcomePage=no
CloseApplications=force

[Languages]
Name: "spanish"; MessagesFile: "compiler:Languages\Spanish.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "..\Release_x64\DDHCenter.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\Release_x64\DDHCenter.exe.config"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\Release_x64\MaterialDesignColors.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\Release_x64\MaterialDesignThemes.Wpf.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\Release_x64\Newtonsoft.Json.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\Release_x64\System.Net.Http.Extensions.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\Release_x64\System.Net.Http.Primitives.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\Release_x64\System.Windows.Interactivity.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\Release_x64\es\System.Windows.Interactivity.resources.dll"; DestDir: "{app}\es"; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
; .NET Framework v4.7.1 offline installer
Source: "C:\Users\Julio\Downloads\NDP472-KB4054530-x86-x64-AllOS-ENU.exe"; DestDir: "{tmp}"; Flags: deleteafterinstall; Check: FrameworkIsNotInstalled; AfterInstall: InstallFramework

[Registry]
Root: "HKLM"; \
    Subkey: "SOFTWARE\Microsoft\Windows\CurrentVersion\Run"; \
    ValueType: string; \
    ValueName: "DDH Center!"; \
    ValueData: """{app}\DDHCenter.exe"" --nowindow"; \
    Flags: createvalueifdoesntexist uninsdeletevalue

[Code]
// Check for installed .NET Framework 4.7.2 in the registry
// MSDN page: https://msdn.microsoft.com/en-us/library/hh925568.aspx
function FrameworkIsNotInstalled: Boolean;
var
  readVal: cardinal;
  success: Boolean;
begin               
  success := RegQueryDWordValue(HKLM, 'SOFTWARE\Microsoft\NET Framework Setup\NDP\v4\Full', 'Release', readVal);
  success := success and (readVal >= 461814);
  Result := not success;
end;

var
    ForceClose: Boolean;

procedure Exterminate;
begin
    ForceClose:= True;
    WizardForm.Close;  
end;

procedure CancelButtonClick(CurPageID: Integer; var Cancel, Confirm: Boolean);
begin
    Confirm:= not ForceClose;
end;

// Create progress dialog for installing .NET Framework 4.7.2
procedure InstallFramework;
var
    StatusText: string;
    ResultCode: Integer;
begin
   StatusText := WizardForm.StatusLabel.Caption;
   WizardForm.StatusLabel.Caption := 'Instalando .NET Framework 4.7.2. Esto podría tomar algunos minutos...';
   WizardForm.ProgressGauge.Style := npbstMarquee;

   try
      if FrameworkIsNotInstalled then
        begin
         Exec(ExpandConstant('{tmp}\NDP472-KB4054530-x86-x64-AllOS-ENU.exe'), '/passive /norestart', '', SW_SHOW, ewWaitUntilTerminated, ResultCode)
         if ResultCode <> 0 then
           begin
               MsgBox('.NET installation failed with code: ' + IntToStr(ResultCode) + '.' + #13#10 + #13#10 + 'Setup will now terminate.', mbError, MB_OK);
               DeleteFile(ExpandConstant('{tmp}\NDP472-KB4054530-x86-x64-AllOS-ENU.exe'));
               Exterminate;
           end
         else
           begin
               WizardForm.StatusLabel.Caption := StatusText;
               WizardForm.ProgressGauge.Style := npbstNormal;
           end;
        end;
   finally
           DeleteFile(ExpandConstant('{tmp}\NDP472-KB4054530-x86-x64-AllOS-ENU.exe'));
   end;
end;
function GetNumber(var temp: String): Integer;
var
  part: String;
  pos1: Integer;
begin
  if Length(temp) = 0 then
  begin
    Result := -1;
    Exit;
  end;
    pos1 := Pos('.', temp);
    if (pos1 = 0) then
    begin
      Result := StrToInt(temp);
    temp := '';
    end
    else
    begin
    part := Copy(temp, 1, pos1 - 1);
      temp := Copy(temp, pos1 + 1, Length(temp));
      Result := StrToInt(part);
    end;
end;
 
function CompareInner(var temp1, temp2: String): Integer;
var
  num1, num2: Integer;
begin
    num1 := GetNumber(temp1);
  num2 := GetNumber(temp2);
  if (num1 = -1) or (num2 = -1) then
  begin
    Result := 0;
    Exit;
  end;
      if (num1 > num2) then
      begin
        Result := 1;
      end
      else if (num1 < num2) then
      begin
        Result := -1;
      end
      else
      begin
        Result := CompareInner(temp1, temp2);
      end;
end;
 
function CompareVersion(str1, str2: String): Integer;
var
  temp1, temp2: String;
begin
    temp1 := str1;
    temp2 := str2;
    Result := CompareInner(temp1, temp2);
end;

function InitializeSetup(): Boolean;
var
  oldVersion: String;
  uninstaller: String;
  ErrorCode: Integer;
begin
  if RegKeyExists(HKEY_LOCAL_MACHINE,
    'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\{7B1487E2-7628-42A3-ACE3-8B55F220E029}_is1') then
  begin
    RegQueryStringValue(HKEY_LOCAL_MACHINE,
      'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\{7B1487E2-7628-42A3-ACE3-8B55F220E029}_is1',
      'DisplayVersion', oldVersion);
    if (CompareVersion('1.0.0.0', oldVersion ) > 0) then
    begin
		RegQueryStringValue(HKEY_LOCAL_MACHINE,
		'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\{7B1487E2-7628-42A3-ACE3-8B55F220E029}_is1',
		'UninstallString', uninstaller);
		ShellExec('runas', uninstaller, '/VERYSILENT', '', SW_HIDE, ewWaitUntilTerminated, ErrorCode);	  
		Result := True;
    end
    else
    begin
      MsgBox('DDH Center! v' + oldVersion + ' ya está instalada.',
        mbInformation, MB_OK);
      Result := False;
    end;
  end
  else
  begin
    Result := True;
  end;
end;


[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{group}\{cm:ProgramOnTheWeb,{#MyAppName}}"; Filename: "{#MyAppURL}"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall
